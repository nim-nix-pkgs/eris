# SPDX-FileCopyrightText: 2020 Emery Hemingway
#
# SPDX-License-Identifier: ISC

{.checks: off, optimization: speed.}

import bitops, endians

const BlockSize* = 64

type
  State = array[16, uint32]
  Block* = array[BlockSize, byte]
  Key* = array[32, byte]
  Nonce* = array[12, byte]
  Counter* = uint32

proc quarterRound(a, b, c, d: var uint32) =
  a = a + b; d = d xor a; d = rotateLeftBits(d, 16)
  c = c + d; b = b xor c; b = rotateLeftBits(b, 12)
  a = a + b; d = d xor a; d = rotateLeftBits(d, 8)
  c = c + d; b = b xor c; b = rotateLeftBits(b, 7)

proc quarterRound(s: var State, x, y, z, w: Natural) =
  quarterRound(s[x], s[y], s[z], s[w])

proc innerBlock(state: var State) =
  quarterRound(state, 0, 4, 8, 12)
  quarterRound(state, 1, 5, 9, 13)
  quarterRound(state, 2, 6, 10, 14)
  quarterRound(state, 3, 7, 11, 15)
  quarterRound(state, 0, 5, 10, 15)
  quarterRound(state, 1, 6, 11, 12)
  quarterRound(state, 2, 7, 8, 13)
  quarterRound(state, 3, 4, 9, 14)

proc init(key: Key; counter: Counter; nonce: Nonce): State =
  result[0] = 0x61707865'u32
  result[1] = 0x3320646e'u32
  result[2] = 0x79622d32'u32
  result[3] = 0x6b206574'u32
  for i in 0..7:
    littleEndian32(addr result[4+i], key[i shl 2].unsafeAddr)
  result[12] = counter
  for i in 0..2:
    littleEndian32(addr result[13+i], nonce[i shl 2].unsafeAddr)

proc chacha20Block(result: var Block; key: Key;
    counter: Counter; nonce: Nonce) =
  var
    state = init(key, counter, nonce)
    initial = state
  for _ in 1..10:
    innerBlock(state)
  for i in 0..15:
    var n = state[i] + initial[i]
    littleEndian32(result[i shl 2].addr, n.addr)

func chacha20*(key: Key; nonce: Nonce; counter: Counter; src, dst: pointer; len: Natural): Counter =
  ## Encrypt or decrypt a buffer. The ``src`` and ``dst`` arguments may be the same buffer.
  var
    blk: Block
    counter = counter
    src = cast[ptr UncheckedArray[byte]](src)
    dst = cast[ptr UncheckedArray[byte]](dst)
  let rem = len and 63
  for j in countup(0, pred(len)-rem, 64):
    chacha20Block(blk, key, counter, nonce)
    inc counter
    for i in countup(j, j or 63):
      dst[i] = src[i].byte xor blk[i and 63]
  if rem != 0:
    chacha20Block(blk, key, counter, nonce)
    for i in countup(len-rem, pred(len)):
      dst[i] = src[i].byte xor blk[i and 63]
  counter

func chacha20*(key: Key; nonce: Nonce; counter: Counter;
    src: openarray[byte]; dst: var openarray[byte]): Counter =
  assert(dst.len == src.len)
  chacha20(key, nonce, counter, unsafeAddr(src[0]), unsafeAddr(dst[0]), dst.len)

func chacha20*(data: string; key: Key; nonce: Nonce; counter = Counter(0)): string =
  ## Encrypt or decrypt a string.
  result = newString(data.len)
  discard chacha20(
      key, nonce, counter,
      data.toOpenArrayByte(data.low, data.high),
      result.toOpenArrayByte(data.low, data.high))

iterator cipherStream*(key: Key; nonce: Nonce; counter = Counter(0)): (Counter, Block) =
  ## Generate a never-ending stream of blocks for the given ``key``, ``nonce``,
  ## and ``counter``. XORing these blocks with data is equivalent to using
  ## ``chacha20`` for encryption and decryption.
  var
    blk: Block
    counter = counter
  while true:
    chacha20Block(blk, key, counter, nonce)
    yield((counter, blk))
    inc counter
