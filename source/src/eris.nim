# SPDX-FileCopyrightText: 2020 Emery Hemingway
#
# SPDX-License-Identifier: ISC

## https://eris.codeberg.page/

import base32, eris/private/chacha20/src/chacha20, eris/private/blake2/blake2

import std/[asyncdispatch, asyncfutures, hashes, math, streams, strutils]

const erisCborTag* = 276

type
  BlockSize* = enum
    ## Valid block sizes.
    bs1k = 1 shl 10
    bs32k = 32 shl 10
  Reference* {.final.} = object ## Reference to an encrypted block.
    bytes*: array[32, byte]
  Key* {.final.} = object ## Key for decrypting a block.
    bytes*: array[32, byte]
  Secret* {.final.} = object ## Secret for salting a `Key`.
    bytes*: array[32, byte]
  Pair {.final, packed.} = object
    r*: Reference
    k*: Key
  ErisCap* = object ## A capability for retrieving ERIS encoded data.
    pair*: Pair
    level*: uint8
    blockSize*: BlockSize

  CorruptionError* = CatchableError

  Cap* {.deprecated: "use ErisCap".} = ErisCap

using
  key: Key
  secret: Secret
  pair: Pair
  cap: ErisCap

assert(sizeOf(Pair) == 64)

func toByte(bs: BlockSize): uint8 =
  case bs
  of bs1k: 0x0a'u8
  of bs32k: 0x0f'u8

func mask(bs: BlockSize; n: int): int = n and bs.int.pred

proc `$`*(x: Reference|Key|Secret): string =
  ## Encode to Base32.
  base32.encode(cast[array[32, char]](x.bytes), pad = false)

proc `==`*(x, y: ErisCap): bool = x.pair.r.bytes == y.pair.r.bytes

proc hash*(r: Reference): Hash =
  for i in 0..<sizeof(Hash): result = result !& r.bytes[i].int
  result = !$result

proc hash*(cap): Hash {.inline.} = hash(cap.pair.r)

const blockSizes = {bs1k.int, bs32k.int}

proc reference*(data: openarray[byte]|seq[byte]|string): Reference =
  ## Derive the `Reference` for a 1KiB or 32KiB buffer.
  assert(data.len in blockSizes)
  var ctx: Blake2b
  ctx.init(32)
  ctx.update(data)
  ctx.final(result.bytes)

proc toBase32*(cap): string =
  var tmp = newSeqOfCap[byte](1+1+32+32)
  tmp.add cap.blockSize.toByte
  tmp.add cap.level.uint8
  tmp.add cap.pair.r.bytes
  tmp.add cap.pair.k.bytes
  base32.encode(cast[seq[char]](tmp), pad = false)

proc `$`*(cap): string =
  ## Encode a ``ErisCap`` to standard URN form.
  ## https://inqlab.net/projects/eris/#_urn
  "urn:erisx3:" & cap.toBase32

proc fromBase32*[T: Reference | Key | Secret](v: var T; s: string): bool =
  try:
    var buf = base32.decode(s)
    if buf.len == v.bytes.len:
      copyMem(v.bytes[0].addr, buf[0].addr, v.bytes.len)
      result = true
  except: discard

proc parseCap*(bin: openArray[char]): ErisCap =
  assert(bin.len == 66)
  result.blockSize =
    case bin[0].byte
    of bs1k.toByte: bs1k
    of bs32k.toByte: bs32k
    else: raise newException(ValueError, "invalid ERIS block size")
  result.level = uint8 bin[1]
  if result.level < 0 or 255 < result.level:
    raise newException(ValueError, "invalid ERIS root level")
  copyMem(addr result.pair.r.bytes[0], unsafeAddr bin[2], 32)
  copyMem(addr result.pair.k.bytes[0], unsafeAddr bin[34], 32)

proc parseErisUrn*(urn: string): ErisCap =
  ## Decode a URN to a ``ErisCap``.
  let parts = urn.split(':')
  if 3 <= parts.len:
    if parts[0] == "urn":
      if parts[1] == "erisx3":
        if parts[2].len >= 106:
          let bin = base32.decode(parts[2][0..105])
          return parseCap(bin)
  raise newException(ValueError, "invalid ERIS URN encoding")

type PutFuture* = FutureVar[seq[byte]]

proc inc(nonce: var Nonce; level: uint8) {.inline.} = nonce[nonce.high] = level

proc encryptLeafFuture(secret; fut: PutFuture): Pair =
  var
    ctx: Blake2b
    nonce: Nonce
  ctx.init(32, secret.bytes)
  ctx.update(fut.mget)
  ctx.final(result.k.bytes)
  discard chacha20(result.k.bytes, nonce, 0, fut.mget, fut.mget)
  ctx.init(32)
  ctx.update(fut.mget)
  ctx.final(result.r.bytes)

proc encryptNodeFuture(level: uint8; fut: PutFuture): Pair =
  const secret = Secret()
  var
    ctx: Blake2b
    nonce: Nonce
  inc(nonce, level)
  ctx.init(32, secret.bytes)
  ctx.update(fut.mget)
  ctx.final(result.k.bytes)
  discard chacha20(result.k.bytes, nonce, 0, fut.mget, fut.mget)
  ctx.init(32)
  ctx.update(fut.mget)
  ctx.final(result.r.bytes)

proc decryptFuture(key; level: uint8; fut: PutFuture) =
  var nonce: Nonce
  inc(nonce, level)
  discard chacha20(key.bytes, nonce, 0, fut.mget, fut.mget)

proc decryptBlock(key; level: uint8; result: var seq[byte]) =
  var nonce: Nonce
  inc(nonce, level)
  discard chacha20(key.bytes, nonce, 0, result, result)

proc verifyBlock(r: Reference; blk: seq[byte]) {.raises: [CorruptionError].} =
  var digest: Reference
  var ctx: Blake2b
  ctx.init(32)
  ctx.update(blk)
  ctx.final(digest.bytes)
  if digest.bytes != r.bytes:
    raise newException(CorruptionError, "ERIS block does not match reference")

proc unpad(blk: var seq[byte]) =
  assert(blk.len in blockSizes)
  for i in countdown(blk.high, blk.low):
    case blk[i]
    of 0x00: discard
    of 0x80:
      blk.setLen(i)
      return
    else: break
  raise newException(IOError, "invalid ERIS block padding")

type
  ErisStore* = ref ErisStoreObj ## Object for interfacing ERIS storage.
  ErisStoreObj* = object of RootObj
    discard

using store: ErisStore

method get*(store; r: Reference): Future[seq[byte]] {.base.} =
  ## Method for getting a block from a ``Store``.
  ## The result is not verified or decrypted.
  result = newFuture[seq[byte]]("ErisStore.get")
  result.fail(newException(KeyError, "get not implemented for this ErisStore"))

method put*(store; r: Reference, f: PutFuture) {.base.} =
  ## Method for putting an encrypted block to a ``Store``.
  assert(f.mget.len in blockSizes)
  cast[Future[void]](f).fail(newException(Defect, "put not implemented for this ErisStore"))

method close*(store) {.base.} = discard
  ## Method for closing a `Store`.

type DiscardStore* = ref object of ErisStoreObj

method put*(s: DiscardStore; r: Reference; f: PutFuture) =
  complete(f)

proc newDiscardStore*(): DiscardStore =
  ## Create an ``ErisStore`` that discards writes and fails to read.
  new(result)

proc put*(store; pf: PutFuture; level: uint8; secret = Secret()): Pair =
  ## Put the plaintext block ``blk`` into ``store`` using an optional ``Secret``.
  ## A ``Pair`` is returned that contains the ``Reference`` and ``Key``
  ## for the combination of  ``blk`` and ``secret``.
  if level == 0:
    result = encryptLeafFuture(secret, pf)
  else:
    result = encryptNodeFuture(level, pf)
  store.put(result.r, pf)

proc get*(store; blockSize: BlockSize; pair; level: uint8):
    Future[seq[byte]] {.async.} =
  ## Get the plaintext block for the reference/key ``pair`` from ``store``.
  ## This procedure verifies that the resulting block matches ``pair``.
  var blk = await get(store, pair.r)
  assert(blk.len == blockSize.int)
  if reference(blk) != pair.r:
    raise newException(Defect, "ERIS block failed verification")
  else:
    if level == 0: verifyBlock(pair.r, blk)
    decryptBlock(pair.k, level, blk)
    return blk

proc splitContent(store; blockSize: BlockSize; secret; content: Stream):
    Future[seq[Pair]] {.async.} =
  var
    pairs = newSeq[Pair]()
    padded = false
    putFut = newFutureVar[seq[byte]]("splitContent")
  putFut.mget.setLen(blockSize.int)
  var count = 0
  while not content.atEnd:
    putFut.mget.setLen content.readData(putFut.mget[0].addr, putFut.mget.len)
    assert(putFut.mget.len <= blockSize.int)
    if unlikely(putFut.mget.len < blockSize.int):
      let i = putFut.mget.len
      inc count
      putFut.mget.setLen(blockSize.int)
      putFut.mget[i] = 0x80
      padded = true
    clean putFut
    pairs.add put(store, putFut, 0, secret)
    await cast[Future[void]](putFut)
  if not padded:
    putFut.mget.setLen(1) # zero all but the first byte
    putFut.mget[0] = 0x80
    putFut.mget.setLen(blockSize.int)
    clean putFut
    pairs.add put(store, putFut, 0, secret)
    await cast[Future[void]](putFut)
  return pairs

func arity*(bs: BlockSize): int = bs.int div sizeOf(Pair)

proc collectRkPairs(store; blockSize: BlockSize; secret; level: uint8; pairs: seq[Pair]):
    Future[seq[Pair]] {.async.} =
  let arity = blockSize.arity
  var
    next = newSeqOfCap[Pair](pairs.len div 2)
    putFut = newFutureVar[seq[byte]]("collectRkPairs")
  putFut.mget.setLen(blockSize.int)
  for i in countup(0, pairs.high, arity):
    let
      pairCount = min(arity, pairs.len - i)
      byteCount = pairCount * sizeof(Pair)
    putFut.mget.setLen(byteCount)
    copyMem(putFut.mget[0].addr, pairs[i].unsafeAddr, byteCount)
    putFut.mget.setLen(blockSize.int)
    clean putFut
    next.add put(store, putFut, level, secret)
  assert(next.len > 0)
  return next

proc encode*(store; blockSize: BlockSize; content: Stream; secret = Secret()):
    Future[ErisCap] {.async.} =
  ## Asychronously encode ``content`` into ``store`` and derive its ``ErisCap``.
  var
    cap = ErisCap(blockSize: blockSize)
    pairs = await splitContent(store, blockSize, secret, content)
  while pairs.len > 1:
    inc(cap.level)
    pairs = await collectRkPairs(store, blockSize, secret, cap.level.uint8, pairs)
  cap.pair = pairs[0]
  return cap

proc encode*(store; blockSize: BlockSize; content: string; secret = Secret()): Future[ErisCap] =
  ## Asychronously encode ``content`` into ``store`` and derive its ``ErisCap``.
  encode(store, blockSize, newStringStream(content), secret)

proc erisCap*(content: string; blockSize: BlockSize; secret = Secret()): ErisCap =
  ## Derive the ``ErisCap`` of ``content``.
  runnableExamples:
    assert:
      $erisCap("Hello world!", bs1k) ==
        "urn:erisx2:AAAD77QDJMFAKZYH2DXBUZYAP3MXZ3DJZVFYQ5DFWC6T65WSFCU5S2IT4YZGJ7AC4SYQMP2DM2ANS2ZTCP3DJJIRV733CRAAHOSWIYZM3M"

  var store = newDiscardStore()
  waitFor encode(store, blockSize, newStringStream(content), secret)
    # DiscardStore will complete this immediately

iterator rk(blk: openarray[byte]): Pair =
  let buf = cast[ptr UncheckedArray[Pair]](blk[0].unsafeAddr)
  block loop:
    for i in countup(0, blk.high, 64):
      block EndCheck:
        for j in i..(i + 63):
          if blk[j] != 0: break EndCheck
        break loop
      yield buf[i div 64]

type
  ErisStream* = ref ErisStreamObj ## An object representing data streams.
  ErisStreamObj = object
    store: ErisStore
    pos: BiggestInt
    leaves: seq[Pair]
    cap: ErisCap
    stopped: bool

proc newErisStream*(store; cap): owned ErisStream =
  ## Open a new stream for reading ERIS data.
  result = ErisStream(
    store: store,
    cap: cap)

proc close*(s: ErisStream) =
  ## Release the resources of an ``ErisStream``.
  reset s.store
  reset s.pos
  reset s.leaves

proc init(s: ErisStream) {.async.} =
  if s.cap.level == 0:
    s.leaves = @[s.cap.pair]
  else:
    let
      arity = s.cap.blockSize.arity
      maxLeaves = arity ^ s.cap.level
    s.leaves = newSeqOfCap[Pair]((maxLeaves div 4) * 3)
      # TODO: math?
    proc expand(level: uint8; pair: Pair) {.async.} =
      # Expand on the stack
      var blk = await get(s.store, s.cap.blockSize, pair, level)
      if level == 1:
        for p in blk.rk:
          s.leaves.add(p)
      else:
        for p in blk.rk:
          await expand(level.pred, p)
    await expand(s.cap.level, s.cap.pair)

proc atEnd*(s: ErisStream): bool = s.stopped
  ## Check if an ``ErisStream`` is positioned at its end.
  ## May return false negatives.

proc setPosition*(s: ErisStream; pos: BiggestInt) =
  ## Seek an ``ErisStream``.
  s.pos = pos
  s.stopped = false

proc getPosition*(s: ErisStream): BiggestInt =
  ## Return the position of an ``ErisStream``.
  s.pos

proc length*(s: ErisStream): Future[BiggestInt] {.async.} =
  ## Estimate the length of an ``ErisStream``.
  ## The result is the length of ``s`` rounded up to the next block boundary.
  await init(s)
  var
    len = s.leaves.len.pred.BiggestInt * s.cap.blockSize.BiggestInt
    lastBlk = await get(s.store, s.cap.blockSize, s.leaves[s.leaves.high], 0)
  unpad(lastBlk)
  return len + lastBlk.len

proc readBuffer*(s: ErisStream; buffer: pointer; bufLen: int): Future[int] {.async.} =
  if s.leaves == @[]: await init(s)
  var
    bNum = s.pos div s.cap.blockSize.int
    buf = cast[ptr UncheckedArray[byte]](buffer)
    bufOff = 0
  while bufOff < bufLen and bNum < s.leaves.len:
    var
      blk = await s.store.get(s.cap.blockSize, s.leaves[bNum], 0)
      blkOff = s.cap.blockSize.mask s.pos.int
    if bNum == s.leaves.high:
      unpad(blk)
      if blk.high < blkOff:
        s.stopped = true
        break
    let n = min(bufLen - bufOff, blk.len - blkOff)
    copyMem(unsafeAddr(buf[bufOff]), addr (blk[blkOff]), n)
    inc(bNum)
    inc(bufOff, n)
    inc(s.pos, n)
  return bufOff

proc read*(s: ErisStream; size: int): Future[seq[byte]] {.async.} =
  var buf = newSeq[byte](size)
  let n = await s.readBuffer(buf[0].addr, buf.len)
  buf.setLen(n)
  return buf

proc readLine*(s: ErisStream): Future[string] {.async.} =
  # TODO: buffer a block?
  if s.leaves == @[]: await init(s)
  var
    line = ""
    bNum = s.pos div s.cap.blockSize.int
  line.setLen(0)
  while true:
    var
      blk = await s.store.get(s.cap.blockSize, s.leaves[bNum], 0)
      blkOff = s.cap.blockSize.mask line.len
    if bNum == s.leaves.high:
      unpad(blk)
    for i in blkOff..blk.high:
      let c = blk[i].char
      if c in Newlines:
        return line
      line.add(c)
    inc(bNum)
    if blk.len < s.cap.blockSize.int:
      return line

proc readDataStr*(s: ErisStream; buffer: var string; slice: Slice[int]): Future[int] =
  readBuffer(s, addr(buffer[slice.a]), slice.b - slice.a)

proc readAll*(s: ErisStream): Future[seq[byte]] {.async.} =
  ## Reads all data from the specified ``ErisStream``.
  while true:
    let blk = await read(s, s.cap.blockSize.int)
    if blk.len == 0: return
    result.add(blk)

proc decode*(store; cap): Future[seq[byte]] =
  ## Asynchronously decode ``cap`` from ``store``.
  readAll(newErisStream(store, cap))

type
  ErisIngest* = ref ErisIngestObj
  ErisIngestObj = object
    ## An object for ingesting data into a store
    store: ErisStore
    future: PutFuture
    leaves: seq[Pair]
    secret: Secret
    pos: BiggestInt
    blockSize: BlockSize

proc newErisIngest*(store: ErisStore; blockSize = bs32k; secret = Secret()): ErisIngest =
  ## Create a new ``ErisIngest`` object.
  result = ErisIngest(
      store: store,
      future: newFutureVar[seq[byte]]("newErisIngest"),
      secret: secret,
      blockSize: blockSize)
  result.future.mget.setLen(blockSize.int)
  result.future.complete()

proc buffer(ingest: ErisIngest): var seq[byte] {.inline.} = ingest.future.mget

proc position*(ingest: ErisIngest): BiggestInt = ingest.pos
  ## Get the current append position of ``ingest``.
  ## This is same as the number of bytes appended.

proc append*(ingest: ErisIngest; data: string|seq[byte]) {.async.} =
  ## Ingest content.
  assert(ingest.future.finished)
  var dataOff = 0
  while dataOff < data.len:
    let
      blkOff = ingest.blockSize.mask ingest.pos.int
      n = min(data.len-dataOff, ingest.blockSize.int-blkOff)
    copyMem(ingest.buffer[blkOff].addr, data[dataOff].unsafeAddr, n)
    ingest.pos.inc n
    dataOff.inc n
    if (ingest.blockSize.mask ingest.pos.int) == 0:
      clean ingest.future
      ingest.leaves.add(ingest.store.put(ingest.future, 0, ingest.secret))
      await cast[Future[void]](ingest.future)

proc append*(ingest: ErisIngest; stream: Stream) {.async.} =
  ## Ingest content from a `Stream`.
  assert(ingest.future.finished)
  var dataOff = 0
  while not stream.atEnd:
    var
      blkOff = ingest.blockSize.mask ingest.pos.int
      n = ingest.blockSize.int - blkOff
    n = readData(stream, ingest.buffer[blkOff].addr, n)
    if n == 0: break
    ingest.pos.inc n
    if (ingest.blockSize.mask ingest.pos.int) == 0:
      clean ingest.future
      ingest.leaves.add(ingest.store.put(ingest.future, 0, ingest.secret))
      await cast[Future[void]](ingest.future)

proc cap*(ingest: ErisIngest): Future[ErisCap] {.async.} =
  ## Derive the ``ErisCap`` of ``ingest``. This proc is idempotent and
  ## will not affect subsequent ``append`` and ``cap`` calls.
  assert(ingest.future.finished)
  var cap = ErisCap(blockSize: ingest.blockSize)
  let padOff = ingest.blockSize.mask ingest.pos.int
  ingest.buffer.setLen(padOff)
  ingest.buffer.setLen(ingest.blockSize.int) # zero the "fresh" buffer space
  ingest.buffer[padOff] = 0x80
  clean ingest.future
  var paddingPair = ingest.store.put(ingest.future, 0, ingest.secret)
  await cast[Future[void]](ingest.future)
  if ingest.leaves.len == 0:
    cap.pair = paddingPair
  else:
    ingest.leaves.add(paddingPair)
    cap.level = 1
    var tree = await collectRkPairs(ingest.store, cap.blockSize, ingest.secret, cap.level, ingest.leaves)
    ingest.leaves.setLen(ingest.leaves.high) # drop the paddingPair for the next run
    while tree.len > 1:
      inc cap.level
      tree = await collectRkPairs(ingest.store, cap.blockSize, ingest.secret, cap.level, move tree)
    cap.pair = tree[0]
  decryptFuture(paddingPair.k, 0, ingest.future)
    # decrypt the ingest state so that more can be appended later
  return cap
